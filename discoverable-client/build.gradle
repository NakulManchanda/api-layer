buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by             : System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number         : System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine        : InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

dependencies {
    implementation(project(':common-service-core'))
    implementation(project(':integration-enabler-spring'))

    implementation libraries.gson
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_boot_starter_web
    implementation libraries.spring_boot_starter_websocket

    //Workaround to fix warning - https://github.com/springfox/springfox/issues/2265
    implementation(libraries.springFox) {
        exclude module: 'swagger-annotations'
        exclude module: 'swagger-models'
    }
    implementation "io.swagger:swagger-annotations:1.5.21"
    implementation "io.swagger:swagger-models:1.5.21"

    implementation libraries.bootstrap
    implementation libraries.jquery
    implementation libraries.find_bugs

    compileOnly libraries.lombok

    testImplementation  libraries.spring_boot_starter_test
}

bootJar.archiveName = "${bootJar.baseName}.jar"
